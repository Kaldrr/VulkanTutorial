cmake_minimum_required(VERSION 3.20)

# Enable Hot Reload for MSVC compilers if supported.
if(POLICY CMP0141)
  cmake_policy(SET CMP0141 NEW)
  set(CMAKE_MSVC_DEBUG_INFORMATION_FORMAT
      "$<IF:$<AND:$<C_COMPILER_ID:MSVC>,$<CXX_COMPILER_ID:MSVC>>,$<$<CONFIG:Debug,RelWithDebInfo>:EditAndContinue>,$<$<CONFIG:Debug,RelWithDebInfo>:ProgramDatabase>>"
  )
endif()

set(CMAKE_CXX_STANDARD 23)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

project(
  VulkanLoadingScreen
  VERSION 0.1
  LANGUAGES CXX)

add_subdirectory(project_options)

option(ENABLE_SANITIZER_ADDRESS "Enable address sanitizer" OFF)
option(ENABLE_SANITIZER_UNDEFINED_BEHAVIOR "Enable UB sanitizer" OFF)
option(ENABLE_SANITIZER_LEAK "Enable leak sanitizer" OFF)
option(ENABLE_SANITIZER_THREAD "Enable thread sanitizer" OFF)
option(ENABLE_SANITIZER_MEMORY "Enable memory sanitizer" OFF)
option(ENABLE_ANALYSIS "Enable analysis" ON)

check_sanitizers_support(SANITIZER_ADDRESS SANITIZER_UNDEFINED_BEHAVIOR
                         SANITIZER_LEAK SANITIZER_THREAD SANITIZER_MEMORY)

if(${ENABLE_SANITIZER_ADDRESS})
  set(USE_SANITIZER_ADDRESS ${SANITIZER_ADDRESS})
endif()
if(${ENABLE_SANITIZER_UNDEFINED_BEHAVIOR})
  set(USE_SANITIZER_UNDEFINED_BEHAVIOR ${SANITIZER_UNDEFINED_BEHAVIOR})
endif()
if(${ENABLE_SANITIZER_LEAK})
  set(USE_SANITIZER_LEAK ${SANITIZER_LEAK})
endif()
if(${ENABLE_SANITIZER_THREAD})
  set(USE_SANITIZER_THREAD ${SANITIZER_THREAD})
endif()
if(${ENABLE_SANITIZER_MEMORY})
  set(USE_SANITIZER_MEMORY ${SANITIZER_MEMORY})
endif()

if(ENABLE_ANALYSIS)
  set(ENABLE_CPPCHECK "ENABLE_CPPCHECK")
  set(ENABLE_CLANG_TIDY "ENABLE_CLANG_TIDY")
  set(ENABLE_VS_ANALYSIS "ENABLE_VS_ANALYSIS")
endif()

project_options(
  PREFIX
  "VulkanLoadingScreen"
  ${ENABLE_CPPCHECK}
  ${ENABLE_CLANG_TIDY}
  ${ENABLE_VS_ANALYSIS}
  ENABLE_NATIVE_OPTIMIZATION
  ${USE_SANITIZER_ADDRESS}
  ${USE_SANITIZER_UNDEFINED_BEHAVIOR}
  ${USE_SANITIZER_LEAK}
  ${USE_SANITIZER_THREAD}
  ${USE_SANITIZER_MEMORY}
  WARNINGS_AS_ERRORS)

add_subdirectory(VulkanLoadingScreen)
